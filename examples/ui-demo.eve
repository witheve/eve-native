# UI Demo

~~~ eve
commit
  [#demo/color color:
    [name: "primary"]
    [name: "secondary"]
    [name: "tertiary"]]

  [#demo/style demo: "ui/toggle" style:
    [name: "default"]
    [name: "square"]
    [name: "track"]]

  [#demo/style demo: "ui/slider" style:
    [name: "default"]
    [name: "square"]
    [name: "track"]]
end
~~~

Set `#demo/color` sorts from eve-auto-index.
~~~ eve
search
  [#demo/color color]
bind
  color.sort += color.eve-auto-index
end
~~~

Set `#demo/style` sorts from eve-auto-index.
~~~ eve
search
  [#demo/style style]
bind
  style.sort += style.eve-auto-index
end
~~~

## Container Demos

Selectable list demo.
~~~ eve
search
  demo = "ui/selectable"

commit
  [#demo name: demo | children:
    [#ui/list #ui/selectable #ui/single-selectable demo | item:
      [#html/div demo text: "first"]
      [#html/div demo text: "second"]
      [#html/div demo text: "third"]
      [#html/div demo text: "fourth"]
      [#html/div demo text: "fifth"]
  ]]
end
~~~

Dropdown demo.
~~~ eve
search
  demo = "ui/dropdown"

commit
  [#demo name: demo | children:
    [#ui/dropdown demo |
      input:
      [#ui/button class: "inset" text: "froof"]
      item:
      [#html/div demo text: "first"]
      [#html/div demo text: "second"]
      [#html/div demo text: "third"]
      [#html/div demo text: "fourth"]
      [#html/div demo text: "fifth"]
  ]]
end
~~~


## Input Demos

Toggle demo.
~~~ eve
search
  demo = "ui/toggle"
  [#demo/color color]
  [#demo/style demo style]

commit
  [#demo name: demo | children:
    [#ui/row demo style | sort: style.sort children:
      [#ui/text sort: 0 text: style.name]
      [#ui/spacer sort: 1]
      [#ui/row #demo/spaced demo style sort: 2 | children:
        [#ui/toggle style color | sort: color.sort class: (style.name color.name)]]]]
end
~~~

Draw demo container.
~~~ eve
search
  demo = [#demo name children]
bind
  [#html/div #demo/root demo | children:
    [#html/element tagname: "h2" text: name]
    [#ui/column #demo/container demo | children]]
end
~~~

~~~ eve
commit
  [#html/style text: "
    .demo-root { max-width: 300px; padding: 20px; margin: 20px; background: white; border-radius: 3px; box-shadow: 0 5px 5px rgba(0, 0, 0, 0.1); }
    .demo-root > h2 { margin-bottom: 20px; font-size: 1.5rem; }
    .demo-container > .ui-row + .ui-row { margin-top: 10px; }
    .ui-row.demo-spaced > * + * { margin-left: 10px; }
    "]
end
~~~

DEBUG
~~~ eve
search
  s = [#ui/selectable cursor]
  s = [#ui/selectable cursor: c2]
  cursor != c2
bind
  [#html/div text: "{{s}} has multiple cursors"]
end
~~~
